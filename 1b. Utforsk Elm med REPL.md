# Utforsk Elm med Elm REPL

Åpne konsollen og skriv: `elm repl`

Da burde du se følgende
```
---- Elm 0.19.1 ----------------------------------------------------------------
Say :help for help and :exit to exit! More at <https://elm-lang.org/0.19.1/repl>
--------------------------------------------------------------------------------
>
```
Nå kan vi utforske Elm-syntaksen

## Aritmetikk

Grunnleggende typer fungerer som forventet:
```elm
> 1 + 1
2 : number
```
Prøv gjerne de forskjellige regneformene:
- \+
- \-
- \*
- \/  (flyttalldivisjon)
- \// (heltallsdivisjon)
- \^ (eksponent)
- sqrt

Og logikk:
- \<
- \>
- \<=
- \>=
- &&
- ||
- not

En komplett liste over grunnleggende funksjoner, finner du her: https://package.elm-lang.org/packages/elm/core/latest/Basics

Legg merke til at Elm repl gir deg både resultatet og typen av resultatet. Vi kommer tilbake til typesystemet senere.

## Strings

Tekst fungerer også som forventet:
```elm
> "Hei"
"Hei" : String
```

Konkatenering:
```elm
> "Hello, " ++ "world!"
"Hello, world!" : String
```

>Oversikt over string-modulen: https://package.elm-lang.org/packages/elm/core/latest/String

## Funksjoner

En grunnleggende funksjon:

```elm
> greeting name =
|   "Hello, " ++ name
|
<function> : String -> String
```

Legg merke til at parametrene til funksjonen ikke omsluttes av paranteser og er mellomromsseparert, *ikke* kommaseparert. Alle funksjoner må returnere noe. Hvis ikke kontroll-flyten tukles med, vil det som regel være siste linje i funksjonen som returneres.

Tilsvarende, når man kaller en funksjon, er argumentene også mellomromsseparert:

```elm
> greeting "Bill"
"Hello, Bill" : String
```

De grunnlegende datastrukturene i Elm har hver sin medfølgende modul med nyttige funksjoner. Disse kan kalles på følgende måte:

```elm
> String.reverse "Bill"
"lliB" : String

> String.length "Bill"
4 : Int
```

>### OPPGAVE:
>Kan du utvide greeting til å ta inn både fornavn og etternavn og hilse med fullstendig navn?

### Anonyme funksjoner
Trenger du bare en liten funksjon her og nå, uten at du vil gi barnet et navn? Anonyme funksjoner er tingen. Disse kan defineres slik:
```elm
> addOne = \n -> n + 1
<function> : number -> number
> addOne 1
2 : number
```

## Expressions

Som et "purely functional" språk, har ikke Elm statements. Bare Expressions. Det vil si at alt må evalueres til "noe". Det må returnere en verdi. En funksjon kan ikke være "void".

Så i Elm finnes det ikke if-statements, bare if-expressions:
```elm
> greeting name =
|   if name == "Dundee" then
|     "G'day mate!"
|   else
|     "Hello " ++ name ++ "!"
|
<function> : String -> String
```
Else-grenene er påkrevd, fordi expressions må returnere en verdi uansett.

En annen type expression som kan være nyttig er "let/in". Dette er kjekk måte å deklarere lokale variabler innenfor en funksjon.

```elm
> greeting name =
|   let
|     start = "Hello "
|     end = "!"
|   in
|     start ++ name ++ end
|
<function> : String -> String

> greeting "Bill"
"Hello Bill!" : String
```


>### OPPGAVE:
>Skriv en greeting-funksjon som tar imot alder og fornavn og gir en passende hilsen avhengig av personens alder.
Tips: if-expressions kan stå rett etter hverandre - if ___ then ___ else if ___ then ___ else ___

## Lister
Vi må ofte håndtere mer enn én av noe. For samlinger av ting har Elm flere datastrukturer: [Array](https://package.elm-lang.org/packages/elm/core/latest/Array), [Dictionary](https://package.elm-lang.org/packages/elm/core/latest/Dict), [List](https://package.elm-lang.org/packages/elm/core/latest/List) og [Set](https://package.elm-lang.org/packages/elm/core/latest/Set). Å velge riktig datastruktur kan være utfordrende noen ganger, men i praksis er det som oftest List man trenger.
En ny liste kan opprettes slik:

```elm
> names = ["Donald", "Barack", "George"]
["Donald","Barack","George"] : List String
```
Legg merke til at vi har gitt listen et navn: "names".
Alle datastrukturene har hver sin modul med nyttige funksjoner. Noen eksempler:
```elm
> List.isEmpty names
False : Bool

List.length names
3 : Int

> List.sort names
["Barack","Donald","George"] : List String

> List.map String.reverse names
["dlanoD","kcaraB","egroeG"] : List String
```
Den siste funksjonen, map, kan være utrolig nyttig og finnes igjen i alle funksjonelle språk. Den tar inn en funksjon og en samling og kjører funksjonen på hvert element i samlingen. Ofte er det her man kan få bruk for anonyme funksjoner:
```elm
> numbers = [1, 2, 3]
[1,2,3] : List number
> List.map (\n -> n +1) numbers
[2,3,4] : List number
```

>### OPPGAVE 1
>Kan man lage en liste med forskjellige typer verdier? Kan man f.eks. blande tall og string i samme liste?

>### OPPGAVE 2
>Lag deg en liste med navn, og skriv ut en hilsen for til hvert av navnene i listen. Bruk f.eks. den første greetings-funksjonen:
```elm
> greeting name =
|   "Hello, " ++ name
|
<function> : String -> String
``` 

# Tuples
Tuples kan bestå av 2 eller 3 verdier og defineres slik:

```elm
> ("Bill",64)
("Bill",64) : ( String, number )
```
I motsetning til lister kan tuples blande alle typer verdier. Til og med andre tupler:

```elm
> (("Bill","Gates"),64, ["Ja","Nei"])
(("Bill","Gates"),64,["Ja","Nei"])
    : ( ( String, String ), number, List String )
```
# Records
Nå snakker vi! Records er virkelig arbeidshesten hva gjelder datastruktiurer i Elm. En record kan holde flere ulike typer verdier, som en tuple, og har ikke noen (praktisk) maksbegrensning på antal verdier. I en record må hver verdi ha et navn. En record kan lages slik:

```elm
> bill = { age = 64, first = "Bill", last = "Gates" }
{ age = 64, first = "Bill", last = "Gates" }
    : { age : number, first : String, last : String }
```
Nå har vi en record som heter bill, og den kan vi gjøre mye morro med!
```elm
> bill.last
"Gates" : String

> bill.age
64 : number

> .last bill
"Gates" : String
```
Ofte ønsker man å oppdatere en record. Dette finnes det en egen syntax for:

```elm
> { bill | age = 30 }
{ age = 30, first = "Bill", last = "Gates" }
    : { age : number, first : String, last : String }
```
Men merk at vi ikke egentlig oppdaterer recorden. Hvis vi nå skriver:
```elm
> bill.age
64 : number
```
Får vi samme resultat som i stad. Hva skjedde?
Svaret er at all data i Elm er immutable. Når noe først er deklarert, kan det aldri endres. Oppdateringssyntaksen vi brukte, oppdaterte ikke recorden "bill", men ga oss en ny record med en annen alder.

>### OPPGAVE 1
>Prøv å ta vare på verdien av en oppdatert record. F.eks.:
> ```elm
>bill2 = { bill | age = 30 }
>```
>Skriv ut bill2.age, hva får du?

>### OPPGAVE 2
>Prøv å sammenligne en record med sin oppdaterte variant. F.eks. "bill == >bill2" Er de like?

>### OPPGAVE 3
>Prøv å opprette to navngitte records med samme felter og verdier. F.eks >lag en bill3 med samme felter som bill. Sammenlign nå "bill == bill3". Er de like? 
