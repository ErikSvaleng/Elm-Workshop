# Moms-kalkulator
Så du har kjøpt noe kult "til inntekts erverelse", og trenger å føre det på SEP. Takk og lov for iBot. Men vent litt, hvordan var det nå egentlig man regnet ut momsen? Skal jeg multiplisere med 0,75? Dele på 1,25?

La oss løse problemet én gang for alle med å lage en moms-kalkulator i Elm hvor man kan skrive inn et beløp i en input og momsen blir automatisk regnet ut og skrevet til skjerm.

Lag en mappe, og **elm-init**, slik som i de to foregående workshoppene.
I Main.elm kan du bruke følgende template som utgangspkt:

```elm
module Main exposing (main)

import Browser
import Html exposing (Html, div, input, text)
import Html.Events exposing (onInput)



-- MAIN


main : Program () Model Msg
main =
    Browser.sandbox { init = init, update = update, view = view }



-- MODEL


type alias Model =
    String


init : Model
init =
    "Hello world!"



-- UPDATE


type Msg
    = Input String



-- update : Msg -> Model -> Model


update : Msg -> Model -> Model
update msg model =
    case msg of
        Input text ->
            model ++ text



-- VIEW


view : Model -> Html Msg
view model =
    div []
        [ text model
        ]
```

<details>
    <summary>Løsningsforslag</summary>

```elm
module Main exposing (main)

import Browser
import Html exposing (Html, div, input, label, span, text)
import Html.Attributes exposing (for, id, placeholder, style, type_, value)
import Html.Events exposing (onInput)



-- MAIN


main : Program () Model Msg
main =
    Browser.sandbox { init = init, update = update, view = view }



-- MODEL
-- Det er tre ting vi ønsker å vise som kan endre seg, så da lagrer vi de i modellen


type alias Model =
    { total : Float
    , productPrice : Float
    , vat : Float
    }


init : Model
init =
    Model 0 0 0 -- initialiserer modellen med record constructor funksjonen


-- UPDATE

-- I dette eksemplet har vi én input, når bruker endrer verdien må vi få en melding om det
-- Vi vedlegger den nye verdien som en string
type Msg
    = TotalChanged String


update : Msg -> Model -> Model
update msg model =
    case msg of
        -- Selv om vi har bare én type melding må vi destruct
        TotalChanged text ->
            --
            let
                parsedTotal =
                    String.toFloat text

                -- toFloat returnerer en Maybe Float (det er jo ikke sikkert strengen kunne parses til en float)
            in
            case parsedTotal of
                -- Må håndtere både gyldig og ugyldig float
                Just total ->
                    if total >= 0 then
                        -- Stay positive!
                        Model total (total / 1.25) (total * 0.2)
                        -- returnerer en ny model med record constructor funksjonen

                    else
                        -- Ignore negativity!
                        model

                Nothing ->
                    Model 0 0 0



-- VIEW


view : Model -> Html Msg
view model =
    div [ style "padding" "30px" ]
        [ label [ for "totalInput" ] [ text "Totalsum: " ]
        , input
            [ onInput TotalChanged
            , value
                (if model.total > 0 then
                    String.fromFloat model.total

                 else
                    ""
                )
            , placeholder "Totalsum"
            , type_ "number"
            , id "totalInput"
            ]
            []
        , span [ style "margin-left" "15px" ] [ text ("Pris eks. mva: " ++ String.fromFloat model.productPrice) ]
        , span [ style "margin-left" "15px" ] [ text ("Mva: " ++ String.fromFloat model.vat) ]
        ]

```
</details>