# Typer

Elm er et statisk typet språk, men man trenger ikke angi typen på alt alltid. Det er fordi kompilatoren bruker *type inference*. Den skanner gjennom alle verdier, hvordan de er deklarert og hvordan de er brukt, for å fastslå hvilke typer de har. I forrige workshop så vi hvordan repl alltid la til typen på resultatet av det vi skrev inn.
F.eks.:
```elm
> "Hei"
"Hei" : String

> bill = { age = 64, first = "Bill", last = "Gates" }
{ age = 64, first = "Bill", last = "Gates" }
    : { age : number, first : String, last : String }

> greeting name =
|   "Hello, " ++ name
|
<function> : String -> String
```
Vi ser f.eks. at Elm har kontroll på at greeting er en funksjon som tar inn en string og returnerer en string (mer om dette senere). Den vet det fordi vi konkatenerer en string med input, "name", til funksjonen, som derfor må være en string. 

>### OPPGAVE
>Prøv å gi greeting et tall som input, hva skjer? 


Selv om funksjoner i Elm strengt tatt bare er vanlige verdier, og noe slikt ...
```elm
> someRecord = { name = "Bill", greeting = greeting }
{ greeting = <function>, name = "Bill" }
    : { greeting : String -> String, name : String }
```
... er fullstendig mulig, tror jeg det vil være pedagogisk å dele inn typesystemet i to: verdier og funksjoner.

# Verdier
String, Int, Bool. Slike grunnleggende verdier kalles for primitiver i Elm. Vi kan også sette disse sammen til mer komplekse typer, som regel records. Som vi har sett i REPL kan typedefinisjonene for disse fort bli ganske lange. Derfor gir Elm oss muligheten til å gi dem et alias.

## Type alias
Et type alias er et kortere navn for en type, f.eks:
```elm
> type alias Person = { age : Int, first : String, last : String }
```
Når man laget et type alias for en record, vil det samtidig genereres en "record constructor"-funksjon. Nå kan vi lage records på denne måten:
```elm
> Person 64 "Bill" "Gates"
{ age = 64, first = "Bill", last = "Gates" }
    : Person
```
>### OPPGAVE 1
>Prøv å lag en et typealias for en record type, og prøv record constructor funksjonen 

>### OPPGAVE 2
>Kan man lage type alias for noe annet enn records? Hva med en tuple? Eller bare en string? Prøv da vel!

>### OPPGAVE 3
>- Definer et type alias for en record (eller bruk en du allerede har definert)
>- Initier en record med constructor funksjonen og assign den: f.eks.
>```elm
> > bill2 = Person 64 "Bill" "Gates"
>{ age = 64, first = "Bill", last = "Gates" }
>    : Person
>```
>- Lag samme record "manuelt" 
>```elm
> > bill = { age = 64, first = "Bill", last = "Gates" }
>```
> - Sammenlign de to recordene, er de like?


## Custom Types
Et type alias er egentlig bare syntaktisk sukker for å unngå for lange typedeklarasjoner. Selv om veldefinerte navn hjelper stort på lesbarheten i kode, utgjør det egentlig ingen forskjell for type checker-en. Dersom vi ønsker å definere egne typer i Elm er det Custom Types som er svaret.

Custom types er det som i andre språk kalles bla. "Union Types", "Variant" og "Algebraic data type". Det er rett og slett en type som definerer en enten-eller datastruktur. Disse kan defineres slik:

```elm
> type User = LoggedIn | Anonymous
> LoggedIn
LoggedIn : User
```
Nå har vi en User-type som kan være enten logget inn, eller anonym. Vi ser at Elm nå gjenkjenner LoggedId som en User. 

Det er også mulig å hekte på data på hvert alternativ, dette kan være hvilken som helst datatype: string, int, tuple, record osv. F.eks. kan vi si at alle inloggede brukere har en epost. Dette kan vi legge til ved å definere User slik:
```elm
> type User = LoggedIn String | Anonymous
> LoggedIn "esv@itema.no"
LoggedIn "esv@itema.no" : User
> LoggedIn
<function> : String -> User
```
>Custom types kan være utrolig nyttig for modellering av state. Vi går ikke inn i avansert bruk i denne workshoppen, men jeg legger her ved noen lenker for dem som vil utforske litt mer avansert bruk av typesystemet i Elm:
>- [Opaque Types](https://medium.com/@ckoster22/advanced-types-in-elm-opaque-types-ec5ec3b84ed2)
>- ["Making Impossible States Impossible" by Richard Feldman](https://www.youtube.com/watch?v=IcgmSRJHu_8)
>- [Kris Jenkins - Slaying a UI Antipattern](https://www.youtube.com/watch?v=NLcRzOyrH08)

>## Et Algebraisk type-system
>Hvis du hører på gata at Elm har det som kalles for et "algebraisk typesystem", betyr det at Elm har sum-typer og produkt-typer.
Records er "produkt typer" det betyr at verdien av en record er produktet av alle feltene i recorden. F.eks. hvis vi har følgende record
>```elm
> > type alias ProductType = { number: Int, isGreat: Bool }
>```
>Kan man si at alle mulige verdier for denne typen er alle heltall * 2 (True og false)
>
>Custom Types er Sum-typer, for den totale verdien typen kan ha er summen av alle alternativene. F.eks.
>```elm
> > type SumType = Number Int | Choice Bool
>```
>Sumtype kan enten være Number eller Choice. Så den totale mengden mulige verdier er alle heltall + 2.
>
>Se gjerne wikipedia for mer utdypende info: https://en.wikipedia.org/wiki/Algebraic_data_type

## Pattern Matching
Nå når vi har kontroll på custom types er det på tide å bli kjent med case-expressions. La oss begynne med å definere et custom type for en brukers kontaktinfo:

```elm
> type Contact = Email String | Phone Int
```
La oss nå si at vi trenger en funksjon for å vise kontaktinfo til brukeren:
```elm
> showContactInfo contact =
|   case contact of
|     Email email -> "Email: " ++ email
|     Phone phone -> "Phone: " ++ (String.fromInt phone)
|
```
Denne kan vi f.eks. bruke slik:
```elm
> contactinfo = Phone 12345678
Phone 12345678 : Contact
> showContactInfo contactinfo
"Phone: 12345678" : String
```
>### OPPGAVE 1
>Skriv en funksjon som tar inn en User (slik vi definerte den lengre opp), og skriver ut en passende hilsen til innloggede og anonyme brukere.

>### OPPGAVE 2
> Skriv en funksjon med en case-expression som bare bruker én av de casene for User. Hva skjer?

# Funksjoner
La oss nå se litt på typesignaturene for funksjoner i Elm.

## Hindley-Milner
For å beskrive typene sine bruker Elm et system som kalles Hindley-Milner. Dette er det skrevet tykke bøker om, og en lang [wikipedia-artikkel](https://en.wikipedia.org/wiki/Hindley%E2%80%93Milner_type_system) med masse matematisk notasjon. For vårt bruk er det nok å tenke på HM som datatype inn -> datatype ut.
F.eks. vår kjære greeting-funksjon hadde følgende type:

```elm
> greeting
<function> : String -> String
```
Greeting er altså en funksjon som tar inn en String og returnerer en String. La oss ta et nytt eksempel fra standardbiblioteket:

```elm
> String.fromInt
<function> : Int -> String
```
String.fromInt tar altså en Int og returnerer en string.

Men hva om vi har to argumenter til en funksjon?

```elm
> add x y = x + y
<function> : number -> number -> number
```

Vi kan lese dette som at add er en funksjon som tar inn en number, og en til number og gir ut enda en number. Hvorfor står det ikke f.eks. følgende? 
```elm
<function> : (number, number) -> number
```
I Elm er dette signaturen til en funksjon som tar inn en tuple av to number og returnerer en number.
>### OPPGAVE
>Bruk REPL og finn ut hva typen til List.length er.

## Type variabler
La du merke til at List.length tok inn en List a? Denne "a" er det vi kaller en typevariabel i Elm. Den indikerer i dette tilfelet at uansett hvilken type elementer som er i listen, så vil List.lenght alltid gi oss riktig svar. Men også at en List kan inneholde hvilken som helst type.

Type variabler må alltid skrives med minuskel (små bokstaver ;-)
Vi kan lage vår egen type med typevariabel:
```elm
> type User a = LoggedIn a | Anonymous
```
Nå kan vi lage en LoggedIn User av hvilken som helst type.
```elm
> LoggedIn "esv@itema.no"
LoggedIn "esv@itema.no" : User String
> LoggedIn 12.01
LoggedIn 12.01 : User Float
> LoggedIn 12
LoggedIn 12 : User number
```
Så fort vi har bestemt en type for a, er det den vi må bruke hele veien. En User String og en User Float er to forskjellige typer. Men hva er egentlig "number"?

>## Constrained Type variables
>I Elm finnes det noen forhåndsdefinerete typevariabler som er begrenset til et set gitt sett med typer. Disse er :
> - number: Int og Float
> - appendable: String og List a
> - comparable: Int, Float, Char, String og lister/tupler av comparables
> - compappend: String og List comparable
Disse eksisterer stort sett for å gjøre operatorer som + og < litt mer fleksible.

## Currying og Partial Application
Dette har ikke noe med krydder å gjøre. Currying er et begrep fra matematikken, og er det det kalles når man gjør om en funksjon som tar flere argumenter til flere funksjoner som tar ett argument hver.
Elm er et auto-curried språk, det vil si at alle funksjoner som tar mer enn ett argument automatisk blir delt opp i flere funksjoner som tar et argument. Så vår kjære add-funksjon
```elm
> add x y = x + y
<function> : number -> number -> number
```
Kan også leses som en funksjon som tar et number og returnerer en ny funksjon som tar et number og returnerer et number. Vi kan også skrive add med en anonym funksjon, slik:
```elm
> add x =
|   \y -> x + y
|
<function> : number -> number -> number
```
Og vi ser at dette gir oss nøyaktig samme type-signatur som vi allerede hadde.

Auto-currying gir oss mulighet for til noe som kalles Partial Application. Det innebærer at vi kan "bake inn" argumenter i funksjoner. F.eks.:
```elm
> add1 = add 1
<function> : number -> number
```
Her tar vi add-funksjonen vår og gir den et enkelt argument. Da får vi tilbake en ny funksjon som tar inn ett number og returnerer ett number. Denne vil alltid legge til 1 til alle tall. Vi kan bruke den slik:
```elm
> add1 3
4 : number
> add1 41
42 : number
```
Husker du List.map? En funksjon som tar inn en annen funksjon og kjører den på hvert element i listen. Som alle funksjoner i Elm, kan man også bruke partial application på denne:

```elm
> add1ToEach = List.map add1
<function> : List number -> List number
> add1ToEach [3,7,9]
[4,8,10] : List number
```
> Funksjoner som tar andre funksjoner som argument kalles ofte [higher order functions](https://en.wikipedia.org/wiki/Higher-order_function)

> ### OPPGAVE
> Kan du lage en funksjon som tar en liste med tall og ganger hver tall med 3, ved å bruke partial application?

### Operatorer er funksjoner
Alle operatorer er også funksjoner i Elm. Dersom vi ønsker å bruke dem som en ren funksjon, kan vi omslutte dem med paranteser. F.eks.:
```elm
> (+)
<function> : number -> number -> number
> (++)
<function> : appendable -> appendable -> appendable
```
Det betyr også at vi kan forenkle add1-funksjonen vår:
```elm
> add1 = (+) 1
<function> : number -> number
```

## Type Annotations
Det er ikke alltid Elm klarer å finne ut av typene på egen hånd, og noen ganger kan den tillate mer enn vi kanskje hadde sett for oss. Ta f.eks. Add1: Hva om vi ønsker å begrense denne til å bare ta inn en Int og returnere en Int? Da kan vi bruke Type Annotations.
```elm
> add2 : Int -> Int
| add2 x = x + 2
|
<function> : Int -> Int
```
Type annotiations har formen 
>funksjonsnavn : type -> type (osv..)

Disste følger altså også "Hindley-Milner"-systemet. Navnet må alltid være likt navnet på funksjonen man annoterer. Det er ansett som best practice i Elm å alltid inkludere Type Annotations med funksjonene man skriver. De hjelper Elm å finne ut hvilke typer som skal inn og ut, forbedrer feilmeldingene og fungerer som en slags dokumentasjon alle som leser koden.

## Komposisjon
Komposisjon. Kjernen i alt funskjonell programmering handler om: Skriv små enkle funksjoner som gjør én ting og gjør den bra, og komponer dem sammen til programmer. (Unix philosophy pkt 1 og 2, bytt ut program med function) Dette kan imidlertid ofte være enklere sagt enn gjort. Vi holder det enkelt i denne workshoppen.

### Pipe-operatoren
Pipe-operatoren tar output fra en funksjon og gir den som input til neste funksjon. Hvis vi ønsker å kombinere add1 med en funksjon som ganger med 3, kan vi bruke pipe-operatoren slik:

```elm
> multi3 = (*) 3
<function> : number -> number
> multi3 3
9 : number
> add1AndMultiplyBy3 x = x |> add1 |> multi3
<function> : number -> number
> add1AndMultiplyBy3 3
12 : number
```
Denne finnes også i revers:
```elm
> multi3AndAdd1 x = add1 <| multi3 <| x
<function> : number -> number
> multi3AndAdd1 3
10 : number
```
### Komposisjonsoperatoren
Komposisjonsoperatoren er lik Pipe-operatoren, men den hekter sammen funksjonene uten at man trenger å bry seg om input.
```elm
> compAdd1AndMulti3 = add1 >> multi3
<function> : number -> number
> compAdd1AndMulti3 3
12 : number
```
Denne finnes også i reversert form:

```elm
> reverseCompAdd1AndMulti3 = add1 << multi3
<function> : number -> number
> reverseCompAdd1AndMulti3 3
10 : number
```
> Å komponere funksjoner slik kalles noen ganger for point-free style. Det betyr at vi ikke nevner hvilken data vi jobber med. Hva er best? Smaken er som baken, den er delt.

> ### OPPGAVE 1
> Kan du lage en funksjon som tar en liste med tall og returnerer en ny liste hvor hvert tall er økt med 1 og ganget med 3, ved å bruke komposisjon? (pipe eller compose)

> ### OPPGAVE 2
> Kan du lage en funksjon som tar en liste med tall og sjekker om hvert tall er svaret på Spørsmålet om livet, universet og alt mulig?